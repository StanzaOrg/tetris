defpackage tetris :
   import core
   import collections

;============================================================
;================== Id Pool =================================
;============================================================

val ID-SEQ = to-seq(0 to false)
val FREE-IDS = Vector<Int>()
val OBJ-MAP = Vector<?>()
public lostanza defn obj-id (x:ref<?>) -> int :
   var id:ref<Int>
   if empty?(FREE-IDS) == true : id = next(ID-SEQ)
   else : id = pop(FREE-IDS)
   set(OBJ-MAP, id, x)
   return id.value

public lostanza defn free-id (id:int) -> ref<False> :
   add(FREE-IDS, new Int{id})
   set(OBJ-MAP, new Int{id}, false)
   return false

public lostanza defn obj (id:int) -> ref<?> :
   return get(OBJ-MAP, new Int{id})

;============================================================   
;=================== QtWidget ===============================
;============================================================

extern QtWidget_new: (ptr<?>) -> ptr<?>
extern QtWidget_delete: (ptr<?>) -> int
extern QtWidget_show: (ptr<?>) -> int
extern QtWidget_set_width: (ptr<?>, int) -> int
extern QtWidget_set_height: (ptr<?>, int) -> int
extern QtWidget_set_listener: (ptr<?>, int) -> int

extern defn QtWidget_paintEvent (listener:int, event:ptr<?>) -> int :
   if listener >= 0 :
      painted(obj(listener) as ref<QtWidgetListener>)
   return 0

extern defn QtWidget_mousePressEvent (listener:int, event:ptr<?>) -> int :
   if listener >= 0 :
      mouse-pressed(obj(listener) as ref<QtWidgetListener>)
   return 0

extern defn QtWidget_mouseReleaseEvent (listener:int, event:ptr<?>) -> int :
   if listener >= 0 :
      mouse-released(obj(listener) as ref<QtWidgetListener>)
   return 0

extern defn QtWidget_mouseMoveEvent (listener:int, event:ptr<?>) -> int :
   if listener >= 0 :
      mouse-moved(obj(listener) as ref<QtWidgetListener>)
   return 0

extern defn QtWidget_keyPressEvent (listener:int, event:ptr<?>) -> int :
   if listener >= 0 :
      key-pressed(obj(listener) as ref<QtWidgetListener>)
   return 0

deftype QtWidgetListener
defmulti painted (l:QtWidgetListener) -> False
defmulti mouse-pressed (l:QtWidgetListener) -> False
defmulti mouse-released (l:QtWidgetListener) -> False
defmulti mouse-moved (l:QtWidgetListener) -> False
defmulti key-pressed (l:QtWidgetListener) -> False

defmethod painted (l:QtWidgetListener) : false
defmethod mouse-pressed (l:QtWidgetListener) : false
defmethod mouse-released (l:QtWidgetListener) : false
defmethod mouse-moved (l:QtWidgetListener) : false
defmethod key-pressed (l:QtWidgetListener) : false

;============================================================
;================= Scratch ==================================
;============================================================

extern input_argc: long
extern input_argv: ptr<ptr<byte>>
extern QApplication_new: (int, ptr<ptr<byte>>) -> ptr<?>
extern QApplication_delete: (ptr<?>) -> int
extern QApplication_exec: (ptr<?>) -> int

defn my-listener () :
   new QtWidgetListener :
      defmethod mouse-pressed (this) :
         println("Mouse pressed")
      defmethod mouse-moved (this) :
         println("Mouse moved")        

println("Calling mymain")
lostanza :
   val app = call-c QApplication_new(input_argc as int, input_argv)

   val widget = call-c QtWidget_new(0L as ptr<?>)
   call-c QtWidget_set_listener(widget, obj-id(my-listener()))
   call-c QtWidget_show(widget)
   
   call-c QApplication_exec(app)
println("Done")


